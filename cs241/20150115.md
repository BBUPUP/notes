# CS 241
## Foundations of Sequential Programs
#### 1/15/2015
Elvin Yung

###  Programs with RAM
* `lw` is the *load word* instruction. It loads a word from RAM into a register.
    * syntax: `lw $a, c($b)`, loads the word at `MEM[c+$b]` into `$a`.
* `sw` is the *store word* instruction. It stores a word from register into RAM.
    * syntax: `sw $a, c($b)`, stores the word at `$a` into the memory location `MEM[c+$b]`.

#### Example: Array indexing
* The register `$1` holds the address of an array, and `$2` holds the length of the array. Retrieve the element with index 5, and store it in register `$3`.

```nasm
lw $3, 20($1)  ; 5*4 = 20
jr $31
```

* Suppose we want to work with arbitrary indices. We introduce the following instructions:
    * `mult` is the *multiply* instruction. Since multiplying two 32-bit numbers might result in a 64-bit number, the results are stored in two special registers `hi` and `lo`.
        * syntax: `mult $a, $b`
    * `div` is the *divide* instruction. The quotient is stroed in `lo`, and the remainder is stored in `hi`.
        * syntax: `div $a, $b`
    * `mfhi` and `mflo` are the *move from HI* and *move from LO* instructions. They move the values from `hi` or `lo` respectively into a given register.
        * syntax: `mfhi $d`, `mflo $d`

```
lis $5  ; load the arbitrary index into $5
.word _
lis $4  ; load the value 4, the size of a word, into $4
.word 4
mult $5, $4  ; obtain the address we need by multiplying the index and the size of a word
mflo $5  ; move the address we need into $5
add $5, $1, $5  ; offset $5 by the address of the array, which was stored at $1
lw $3, 0($5)  ; load from memory the address we need, into $3
jr $31
```

### Labels
* Recall, in the previous loop example (cf. [notes from 01/13](20151113.md), *Sum the integers `1..13`, store in `$3`, then return*) that the `lis` command was inside the loop, which could be moved outside.
* That is fine, but the `bne` at the end now has an improper immediate/offset (should be -3 instead of -5). 
* In nested loops/branches, this is bad. Since we hardcoded branch offsets, we need to change them every time we add or remove instructions inside a loop.
* Instead, the assembler allows for *labelled* instructions: `<label>: <instruction>`
* The assembler dynamically associates the name (label) with the instruction that follows.
* Now we can rewrite the loop example with a label:

```nasm
0x00 add $3, $0, $0
0x04 lis $2
0x08 .word 13
0x0c lis $1
0x10 .word 1
    top:  ; the value of `top` is 0x14
0x14 add $3, $3, $2
0x18 sub $2, $2, $1
0x1c bne $2, $0, top  ; `top` is provided, rather than an explicit offset
0x20 jr $31
```

* For most intents and purposes, the value of `top` in the example above is `0x14`.
* However, when the assembler sees a label in a branching instruction (`beq` or `bne`), it computes the relative difference between PC and top, in terms of words: `(label - PC) / 4`

### Procedures
* Procedures in assembly allows us to reuse code.
* However, we have two problems to solve:
    * How to transfer control in and out of the procedure, parameter passing, etc.
    * How to make sure that procedures don't overwrite important data
* Let's deal with the second problem first. 
* We could reserve registers for the prodcedure `f`, and some for mainline with no overlap, but if procedures call other procedures or themselves, we run out of registers. 
* Instead, we will allow procedures to do whatever they want with registers, as long as it puts them back to their original values on exit.
* Thus we need to use RAM.
* To prevent procedures from using the same RAM addresses, we use a **stack pointer**. The stack pointer is located at register `$30`, which is the other restricted register that we previously discussed.
* Essentially, we allocate memory from the top or bottom of free RAM, and somehow keep track of what RAM isn't used. 
* The machine helps us out: `$30` is initialized (by the lowader) to just past the last word of memory. 
* `$30` basically acts as a "bookmark" of sorts to separate used and unused RAM, if we allocate from the bottom.
* We use RAM like a stack, pushing upon function calls and popping upon returns. 
* Our strategy: each procedure pushes the registers it will use onto the stack and pops the origin values from the stack when finished.
* `$30`, the stack pointer, contains the address of the top of the stack.

#### Template for Procedures
1. Push registers function will modify onto the stack.
2. Decrement stack pointer ($30) accordingly.
3. Execute function, which should only change the saved registers.
4. Increment stack pointer `$30`.
5. Pop registers off the stack.
6. Return.

#### Example
* Suppose a function `f` only uses `$2` and `$3`. 

```nasm
f: 
    ; store registers we'll be using into memory
    sw $2, -4($30)
    sw $3, -8($30)

    ; decreent stack pointer
    lis $3
    .word 8
    sub $30, $30, $3

    ; FUNCTION BODY GOES HERE
    ...

    ; pop registers off stack
    ; move stack pointer back, load stored registers, return
    add $30, $30, $3
    lw $3, -8($30)
    lw $2, -4 ($30)
    jr $31
```

